import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
        classpath 'com.bmuschko:gradle-izpack-plugin:2.1'
    }
}

repositories { mavenCentral() }

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'com.bmuschko.izpack'

sourceCompatibility = 1.7
version = '1.2.0'

applicationName = 'OneClipboard'
def jarName = applicationName + '.jar'

dependencies {
    compile 'com.outr.javasysmon:javasysmon_2.10:0.3.4'
    compile 'org.bouncycastle:bcprov-jdk15on:1.46'
    compile project(':oneclipboardlib')
    izpack 'org.codehaus.izpack:izpack-standalone-compiler:4.3.5'
}

sourceSets.main.java.srcDirs = ['src/main/java']
sourceSets.main.resources.srcDirs = ['src/main/resources']

mainClassName = "com.cb.oneclipboard.desktop.Client"

processResources {
    filesMatching('**/config.properties') {
        filter ReplaceTokens, tokens: [
                "serverHostName"           : project.serverHostName,
                "version"                  : version
        ]
    }
}

distZip {
    archiveName applicationName + ".zip"
    // Avoid parent directory in zip
    eachFile { file ->
        String path = file.relativePath
        file.setPath(path.substring(path.indexOf("/") + 1, path.length()))
    }
}

task izPackAssemble(type: Copy) {
    from 'src/main/izpack'
    into "$buildDir/assemble/izpack"
}

izpack {
    izPackCreateInstaller.dependsOn distZip, izPackAssemble
    appProperties = [
            'app.name'           : applicationName,
            'app.winStartScript' : "bin\\$applicationName" + '.bat',
            'app.unixStartScript': "bin/$applicationName",
            'app.version'        : version,
            'app.archive.path'   : "$buildDir/distributions/$distZip.archiveName",
            'app.author.name'    : project.authorName,
            'app.author.email'   : project.authorEmail,
            'app.website'        : project.appWebsite,
            'app.logo'           : rootDir.absolutePath + File.separator + "logo.png",
            'app.ico'           : rootDir.absolutePath + File.separator + "logo.ico"
    ]
}
